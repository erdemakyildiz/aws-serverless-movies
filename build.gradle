import static org.gradle.api.JavaVersion.VERSION_1_8

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
}

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

ext {
    sourceCompatibility = VERSION_1_8
    targetCompatibility = VERSION_1_8
}

repositories {
    jcenter()
    maven {
        url 'http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release'
    }
}

version = '0.1'
group = 'be.houbrechts-it'


dependencies {
    compileOnly('org.projectlombok:lombok:1.16.20')
    compileOnly('com.amazonaws:aws-lambda-java-core:1.2.0')
    compileOnly('com.amazonaws:aws-lambda-java-events:1.2.0')
    compile('com.amazonaws:aws-java-sdk-lambda:1.11.259')
    compile('com.amazonaws:aws-java-sdk-dynamodb:1.11.259')
    compile('com.google.code.gson:gson:2.3.1')

    testCompile('com.amazonaws:aws-lambda-java-core:1.2.0')
    testCompile('com.amazonaws:aws-java-sdk-lambda:1.2.0')
    testCompile('com.amazonaws:DynamoDBLocal:1.11.86')
    testCompile('junit:junit:4.12')
}

shadowJar {
    classifier = null //overwrite the original jar
}

tasks.withType(Test) {
    environment.AWS_ACCESS_KEY_ID = 'id'
    environment.AWS_SECRET_ACCESS_KEY = 'key'
    //Pass all system properties to the Test JVM
    systemProperties = System.getProperties()
    systemProperties['user.dir'] = workingDir
    systemProperties['java.library.path'] = 'build/test-libs'
}

test {
    useJUnit {}
}

task copyLocalDynamoDependencies(type: Copy) {
    from(configurations.testCompile) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/test-libs'
}
test.dependsOn copyLocalDynamoDependencies
